plugins {
    id 'java'
    id 'application'
    id 'eclipse'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.graalvm.plugin.compiler" version "0.1.0-alpha2"
    id 'com.bmuschko.docker-java-application' version '7.3.0'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.10'
}

ext.graalVersion = '22.0.0.2'

group 'fr.thesimpleteam'
version '1.0'

application {
    mainClassName = "net.thesimpleteam.simplebot.SimpleBot"
    tasks.run.setWorkingDir(file(sourceSets.main.resources.srcDirs[0]))
    run {
        getSystemProperties().put("simplebot.dev", "true")
    }
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = 'UTF-8'


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

import java.util.concurrent.Callable

graal {
    version "$graalVersion"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://m2.chew.pro/releases" }
}

dependencies {
    //JDA & JDA-Utilities
    implementation("net.dv8tion:JDA:4.4.1_353")
    implementation('pw.chew:jda-chewtils:1.23.0') {
        exclude group: 'pw.chew', module: 'jda-chewtils-command'
    }
    implementation 'com.github.TheSimpleTeam.JDA-Chewtils:jda-chewtils-command:ab05b178ae'
    //ByteCode
    implementation 'net.bytebuddy:byte-buddy:1.15.5'
    //Eval
    implementation "org.graalvm.sdk:graal-sdk:$graalVersion"
    implementation "org.graalvm.js:js:$graalVersion"
    implementation "org.graalvm.js:js-scriptengine:$graalVersion"
    implementation('com.caoccao.javet:javet:1.1.4')
    implementation 'org.python:jython-standalone:2.7.2'
    //CLI
    implementation 'commons-cli:commons-cli:1.5.0'
    //API
    implementation('io.ktor:ktor-server-core:1.6.8')
    implementation('io.ktor:ktor-server-host-common:1.6.8')
    implementation('io.ktor:ktor-server-netty:1.6.8')
    implementation('io.ktor:ktor-serialization:1.6.8')
    //Mixins
    implementation 'com.github.ReflxctionDev.Tuna-Bytes:core:1.2.0'
    annotationProcessor 'com.github.ReflxctionDev.Tuna-Bytes:core:1.2.0'
    //Other
    implementation 'com.github.Thesimpleteam:spiget:15c1d27021'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation("com.google.guava:guava:31.1-jre")
    implementation group: 'org.kohsuke', name: 'github-api', version: '1.306'
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation "org.mariuszgromada.math:MathParser.org-mXparser:5.2.1"
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'sh.stein:carbon-api:1.2'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.6.21'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

docker {
    javaApplication {
        baseImage = ''
        getApiVersion().set("1.0")
        maintainer = 'Minemobs <minemobs.pro@gmail.com>'
        jvmArgs = ['-Xms256m', '-Xmx1024m']
    }
}

task copyLangs(type: Copy) {
    from('lang')
    into("$buildDir/docker/lang")
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources"
        }
    }
}

task createDockerFile(type: Dockerfile) {
    dependsOn(clean)
    dependsOn(dockerSyncBuildContext)
    finalizedBy(copyLangs)
    from('eclipse-temurin:17_35-jdk-focal')
    label([MAINTAINER: "Minemobs <minemobs.pro@gmail.com>"])
    workingDir("/app")
    addFile("lang", "lang/")
    addFile("libs", "libs/")
    addFile("classes", "classes/")
    entryPoint(project.provider(new Callable<List<String>>() {
        @Override 
        List<String> call() throws Exception {
            ["java", "-Xms256m", "-Xmx1024m", "-cp", "/app/resources:/app/classes:/app/libs/*", "net.thesimpleteam.simplebot.SimpleBot"]
        }
    }))
    exposePort(8080)
}

task buildDocker(type: DockerBuildImage) {
    dependsOn createDockerFile
    dockerFile.set(file("$buildDir/docker/Dockerfile"))
    images.add("thesimpleteam/$project.name:$project.version")
}

shadowJar {
    exclude 'plugins'
    exclude 'config'
    exclude 'logs'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "17"
    }
}
